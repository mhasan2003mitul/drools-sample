package chapter04.ruleExceptions

import org.drools.devguide.eshop.model.Order;
import org.drools.devguide.eshop.types.IsLargeOrder;

rule "large orders" @Defeasible
    when Order($id: orderId, $total: total > 150.00)
    then
        System.out.println("large orders "+$total);
        insertLogical(new IsLargeOrder($id));
end

rule "large orders exception"
    @Defeats( "large orders" )
    when Order($id: orderId,$total: total > 150.00, totalItems < 5)
    then
        System.out.println("large orders exception "+$total);
        insertLogical(new IsLargeOrder($id),"neg");
end

rule "large orders double exception"
  @Defeats( "large orders exception" )
    when Order($id: orderId,total > 300.00)
    then
        System.out.println("large orders double exception ");
        insertLogical(new IsLargeOrder($id));
end

rule "print large orders after processing"
    salience -10
    when $o: IsLargeOrder()
    then System.out.println($o);
end

